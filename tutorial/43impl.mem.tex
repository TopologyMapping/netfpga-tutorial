\subsection{Acesso à memória SRAM}
\label{sec:impl.mem}

Nosso módulo utiliza a memória SRAM para armazenar as portas
bloqueadas e decidir quais pacotes filtrar.  No sexto estado do
processamento de um pacote emitimos uma requisição de leitura para o
endereço \ssf{SRAM\_PORTS\_ADDR}, que contem as portas TCP
bloqueadas.  No sétimo estado esperamos a leitura completar e então
utilizamos o dado lido para verificar se o pacote precisa ser
descartado ou não.

Nosso \emph{firewall} emite operações de leitura da memória para o
módulo \ssf{sram\_arbiter}, que intermedia o acesso à memória SRAM.
As linhas de comunicação do nosso \emph{firewall} com o
\ssf{sram\_arbiter} ilustram a interface de acesso à memória.

\begin{verilogcode}
      output reg                       sram_rd_req,
      output reg [SRAM_ADDR_WIDTH-1:0] sram_rd_addr,
      input [DATA_WIDTH-1:0]           sram_rd_data,
      input                            sram_rd_ack,
      input                            sram_rd_vld,
      output reg                       sram_wr_req,
      output reg [SRAM_ADDR_WIDTH-1:0] sram_wr_addr,
      output reg [DATA_WIDTH-1:0]      sram_wr_data,
      input                            sram_wr_ack,
\end{verilogcode}

O \ssf{sram\_arbiter} pode receber uma requisição de leitura ou
escrita por ciclo de relógio.  Requisições de leitura são indicadas
ligando \ssf{sram\_rd\_req} e informando o endereço a ser lido em
\ssf{sram\_rd\_addr}.  No próximo ciclo de relógio o
\ssf{sram\_arbiter} indica se a requisição foi recebida com sucesso
ligando o sinal \ssf{sram\_rd\_ack}.  Como leituras demoram alguns
ciclos para serem atendidas, o módulo que pediu a leitura deve
esperar os dados serem retornados e disponibilizados pelo
\ssf{sram\_arbiter}.  O \ssf{sram\_arbiter} informa que os dados
estão disponíveis em \ssf{sram\_rd\_data} ligando
\ssf{sram\_rd\_vld}.

Requisições de escrita são indicadas ligando \ssf{sram\_wr\_req},
informando o endereço a ser escrito em \ssf{sram\_wr\_addr} e
informando o dado a ser escrito em \ssf{sram\_wr\_data}.  O
\ssf{sram\_arbiter} indica se a requisição foi recebida com sucesso
ligando o sinal \ssf{sram\_wr\_ack}.  Como o dado a ser escrito é
armazenado pelo \ssf{sram\_arbiter}, o módulo que fez a requisição
de escrita não precisa esperar mais nenhuma confirmação do
\ssf{sram\_arbiter}.  Se ambos os sinais \ssf{sram\_rd\_req} e
\ssf{sram\_wr\_req} estiverem ligados, nosso \ssf{sram\_arbiter}
prioriza a requisição de escrita.  A SRAM usada na NetFPGA garante
que leituras realizadas após escritas lerão o dado atualizado.

A interface que exportamos em nosso \ssf{sram\_arbiter} é
simplificada.  Como descrito na seção~\ref{sec:arch.hw}, a NetFPGA
possui dois bancos de memórias SRAM, cada um com $2^{19}$ linhas de
36~bits.  Nosso \ssf{sram\_arbiter} combina os dois bancos para
apresentar uma abstração de memória de $2^{19}$ linhas de 64~bits.
Usamos 8~bits de cada linha como bits de paridade, calculados e
verificados automaticamente pelo \ssf{sram\_arbiter}.

\begin{verilogcode}
   // sram_arbiter.v
   generate
      genvar m;
      for(m = 0; m < 8; m = m+1) begin: calc_par_bits
      assign parbit[m] = wr_data[m*8] ^ wr_data[m*8+1] ^
            wr_data[m*8+2] ^ wr_data[m*8+3] ^ wr_data[m*8+4] ^
            wr_data[m*8+5] ^ wr_data[m*8+6] ^ wr_data[m*8+7];
      end // wr_data is 64 bits wide
   endgenerate 
   generate
      genvar l;
      for(l = 0; l < 8; l = l+1) begin: expand_wr_data
         assign wr_data_exp[(l+1)*9-1 : l*9] =
            {wr_data[(l+1)*8-1:l*8], parbit[l]};
         end // wr_data_exp is 72 bits wide (36*2)
   endgenerate
\end{verilogcode}

Para acessar as duas memórias simultaneamente duplicamos os sinais
de requisição de escrita ou leitura e os endereços para os dois
bancos de memória.  Para requisições de escrita escrevemos metade
dos dados em cada banco e para requisições de leitura concatenamos
os dados dos dois bancos.  Abaixo mostramos o código para realizar
estas operações.  Este código fica dentro do módulo \ssf{nf2\_core}.
O módulo \ssf{nf2\_core} é o módulo raiz do \emph{software} da
NetFPGA e comunica diretamente com os pinos do FPGA.  O
\ssf{nf2\_core} conecta os pinos do FPGA conectados às memórias SRAM
ao \ssf{sram\_arbiter} da seguinte forma.

\begin{verilogcode}
// nf2_core.v
// hardware pins       sram_arbiter
assign sram1_wr_data = wr_data_exp[`SRAM_DATA_WIDTH-1:0];
assign sram2_wr_data = wr_data_exp[2*`SRAM_DATA_WIDTH-1:`SRAM_DATA_WIDTH];
assign sram1_we      = sram_we; // 0 for write, 1 for read
assign sram2_we      = sram_we;
assign sram1_addr    = sram_addr;
assign sram2_addr    = sram_addr;
// sram_arbiter        hardware pins
assign sram_rd_data  = {sram2_rd_data, sram1_rd_data};
\end{verilogcode}

O \ssf{sram\_arbiter} pode ser modificado para permitir acesso mais
eficiente à memória caso a aplicação tenha um padrão específico de
acessos.  Por exemplo, é possível modificar as atribuições acima
para permitir ler endereços distintos em cada banco de SRAM.  A SRAM
também provê um mecanismo para permitir escritas parciais,
escolhendo quais bytes devem ser escritos em uma requisição de
escrita.\footnotemark{}

\footnotetext{Não mostramos esta funcionalidade no texto.  Nossa
implementação não suporta escritas parciais.  Escritas parciais
poderiam ser controladas configurando o valor das linhas
\sssf{sram\_bw} no \sssf{sram\_arbiter}.}

Para exemplificar o controle de acesso à SRAM num nível mais baixo,
iremos explicar o tratamento de uma requisição de leitura
(requisições de escrita são mais simples).  Quando o
\ssf{sram\_arbiter} recebe uma requisição de leitura, ele desabilita
escrita ligando o sinal \ssf{sram\_we} (este sinal possui lógica
negativa), repassa o endereço a ser lido ao \emph{hardware} e
confirma a requisição de leitura.

\begin{verilogcode}
   // sram_arbiter.v
   else if(sram_rd_req) begin
      hw_we <= 1'b1;                // read
      hw_addr <= sram_rd_addr;
      sram_rd_ack <= sram_rd_req;   // acknowledge read request
      sram_wr_ack <= 0;             // do not acknowledge write
      rd_vld_early3 <= sram_rd_req; // data back in three cycles
      ...
   end
\end{verilogcode}

Como o dado demora dois ciclos para ser retornado da SRAM após a
requisição, o \ssf{sram\_arbiter} possui um \emph{pipeline} interno
para esperar os dados serem retornados pela SRAM.  Após dois ciclos
o \ssf{sram\_arbiter} armazena o dado lido no registrador
\ssf{sram\_rd\_data} e encaminha este registrador para o
\emph{firewall} no terceiro ciclo de relógio após a requisição.

\begin{verilogcode}
   // sram_arbiter.v
   rd_vld_early2 <= rd_vld_early3; // waited 1
   rd_vld_early1 <= rd_vld_early2; // waited 2
   if(rd_vld_early1) begin // memory sending data this cycle, storing
      if(parity_check)
         sram_rd_data <= rd_data_exp_parsed; // no parity bits
      else
         sram_rd_data <= 64'hdeadfeeddeadfeed;
   end
   sram_rd_vld <= rd_vld_early1;   // data is here, set valid bit
\end{verilogcode}
